-- MAP OBJECT --

	object_const_def
	const_export POKEMONFANCLUB_PIKACHU_FAN
	const_export POKEMONFANCLUB_SEEL_FAN
	const_export POKEMONFANCLUB_PIKACHU
	const_export POKEMONFANCLUB_SEEL
	const_export POKEMONFANCLUB_CHAIRMAN
	const_export POKEMONFANCLUB_RECEPTIONIST

PokemonFanClub_Object:
	db $d ; border block

	def_warp_events
	warp_event  2,  7, LAST_MAP, 2
	warp_event  3,  7, LAST_MAP, 2

	def_bg_events
	bg_event  1,  0, TEXT_POKEMONFANCLUB_SIGN_1
	bg_event  6,  0, TEXT_POKEMONFANCLUB_SIGN_2

	def_object_events
	object_event  6,  3, SPRITE_FISHER, STAY, LEFT, TEXT_POKEMONFANCLUB_PIKACHU_FAN
	object_event  1,  3, SPRITE_GIRL, STAY, RIGHT, TEXT_POKEMONFANCLUB_SEEL_FAN
	object_event  6,  4, SPRITE_FAIRY, STAY, LEFT, TEXT_POKEMONFANCLUB_PIKACHU
	object_event  1,  4, SPRITE_SEEL, STAY, RIGHT, TEXT_POKEMONFANCLUB_SEEL
	object_event  3,  1, SPRITE_GENTLEMAN, STAY, DOWN, TEXT_POKEMONFANCLUB_CHAIRMAN
	object_event  5,  1, SPRITE_LINK_RECEPTIONIST, STAY, DOWN, TEXT_POKEMONFANCLUB_RECEPTIONIST

	def_warps_to POKEMON_FAN_CLUB


-- MAP HEADER --

	map_header PokemonFanClub, POKEMON_FAN_CLUB, INTERIOR, 0
	end_map_header


-- MAP TEXT --

_PokemonFanClubPikachuFanNormalText::
	text "Won't you admire"
	line "my PIKACHU's"
	cont "adorable tail?"
	done

_PokemonFanClubPikachuFanBetterText::
	text "Humph! My PIKACHU"
	line "is twice as cute"
	cont "as that one!"
	done

_PokemonFanClubSeelFanNormalText::
	text "I just love my"
	line "SEEL!"

	para "It squeals when I"
	line "hug it!"
	done

_PokemonFanClubSeelFanBetterText::
	text "Oh dear!"

	para "My SEEL is far"
	line "more attractive!"
	done

_PokemonFanClubPikachuText::
	text "PIKACHU: Chu!"
	line "Pikachu!"
	done

_PokemonFanClubSeelText::
	text "SEEL: Kyuoo!"
	done

_PokemonFanClubChairmanIntroText::
	text "I chair the"
	line "#MON Fan Club!"

	para "I have collected"
	line "over 100 #MON!"

	para "I'm very fussy"
	line "when it comes to"
	cont "#MON!"

	para "So..."

	para "Did you come"
	line "visit to hear"
	cont "about my #MON?"
	done

_PokemonFanClubChairmanStoryText::
	text "Good!"
	line "Then listen up!"

	para "My favorite"
	line "RAPIDASH..."

	para "It...cute..."
	line "lovely...smart..."
	cont "plus...amazing..."
	cont "you think so?..."
	cont "oh yes...it..."
	cont "stunning..."
	cont "kindly..."
	cont "love it!"

	para "Hug it...when..."
	cont "sleeping...warm"
	cont "and cuddly..."
	cont "spectacular..."
	cont "ravishing..."
	cont "...Oops! Look at"
	cont "the time! I kept"
	cont "you too long!"

	para "Thanks for hearing"
	line "me out! I want"
	cont "you to have this!"
	prompt

_PokemonFanClubReceivedBikeVoucherText::
	text "<PLAYER> received"
	line "a @"
	text_ram wStringBuffer
	text "!@"
	text_end

_PokemonFanClubExplainBikeVoucherText::
	text_start

	para "Exchange that for"
	line "a BICYCLE!"

	para "Don't worry, my"
	line "FEAROW will FLY"
	cont "me anywhere!"

	para "So, I don't need a"
	line "BICYCLE!"

	para "I hope you like"
	line "cycling!"
	done

_PokemonFanClubNoStoryText::
	text "Oh. Come back"
	line "when you want to"
	cont "hear my story!"
	done

_PokemonFanClubChairFinalText::
	text "Hello, <PLAYER>!"

	para "Did you come see"
	line "me about my"
	cont "#MON again?"

	para "No? Too bad!"
	done

_PokemonFanClubBagFullText::
	text "Make room for"
	line "this!"
	done

_PokemonFanClubReceptionistText::
	text "Our Chairman is"
	line "very vocal about"
	cont "#MON."
	done

_PokemonFanClubSign1Text::
	text "Let's all listen"
	line "politely to other"
	cont "trainers!"
	done

_PokemonFanClubSign2Text::
	text "If someone brags,"
	line "brag right back!"
	done


-- MAP SCRIPT --

PokemonFanClub_Script:
	jp EnableAutoTextBoxDrawing

PokemonFanClub_CheckBikeInBag:
; check if any bike paraphernalia in bag
	CheckEvent EVENT_GOT_BIKE_VOUCHER
	ret nz
	ld b, BICYCLE
	call IsItemInBag
	ret nz
	ld b, BIKE_VOUCHER
	jp IsItemInBag

PokemonFanClub_TextPointers:
	def_text_pointers
	dw_const PokemonFanClubPikachuFanText,   TEXT_POKEMONFANCLUB_PIKACHU_FAN
	dw_const PokemonFanClubSeelFanText,      TEXT_POKEMONFANCLUB_SEEL_FAN
	dw_const PokemonFanClubPikachuText,      TEXT_POKEMONFANCLUB_PIKACHU
	dw_const PokemonFanClubSeelText,         TEXT_POKEMONFANCLUB_SEEL
	dw_const PokemonFanClubChairmanText,     TEXT_POKEMONFANCLUB_CHAIRMAN
	dw_const PokemonFanClubReceptionistText, TEXT_POKEMONFANCLUB_RECEPTIONIST
	dw_const PokemonFanClubSign1Text,        TEXT_POKEMONFANCLUB_SIGN_1
	dw_const PokemonFanClubSign2Text,        TEXT_POKEMONFANCLUB_SIGN_2

PokemonFanClubPikachuFanText:
	text_asm
	CheckEvent EVENT_PIKACHU_FAN_BOAST
	jr nz, .mineisbetter
	ld hl, .NormalText
	call PrintText
	SetEvent EVENT_SEEL_FAN_BOAST
	jr .done
.mineisbetter
	ld hl, .BetterText
	call PrintText
	ResetEvent EVENT_PIKACHU_FAN_BOAST
.done
	jp TextScriptEnd

.NormalText:
	text_far _PokemonFanClubPikachuFanNormalText
	text_end

.BetterText:
	text_far _PokemonFanClubPikachuFanBetterText
	text_end

PokemonFanClubSeelFanText:
	text_asm
	CheckEvent EVENT_SEEL_FAN_BOAST
	jr nz, .mineisbetter
	ld hl, .NormalText
	call PrintText
	SetEvent EVENT_PIKACHU_FAN_BOAST
	jr .done
.mineisbetter
	ld hl, .BetterText
	call PrintText
	ResetEvent EVENT_SEEL_FAN_BOAST
.done
	jp TextScriptEnd

.NormalText:
	text_far _PokemonFanClubSeelFanNormalText
	text_end

.BetterText:
	text_far _PokemonFanClubSeelFanBetterText
	text_end

PokemonFanClubPikachuText:
	text_asm
	ld hl, .Text
	call PrintText
	ld a, PIKACHU
	call PlayCry
	call WaitForSoundToFinish
	jp TextScriptEnd

.Text
	text_far _PokemonFanClubPikachuText
	text_end

PokemonFanClubSeelText:
	text_asm
	ld hl, .Text
	call PrintText
	ld a, SEEL
	call PlayCry
	call WaitForSoundToFinish
	jp TextScriptEnd

.Text:
	text_far _PokemonFanClubSeelText
	text_end

PokemonFanClubChairmanText:
	text_asm
	call PokemonFanClub_CheckBikeInBag
	jr nz, .nothingleft

	ld hl, .IntroText
	call PrintText
	call YesNoChoice
	ld a, [wCurrentMenuItem]
	and a
	jr nz, .nothanks

	; tell the story
	ld hl, .StoryText
	call PrintText
	lb bc, BIKE_VOUCHER, 1
	call GiveItem
	jr nc, .bag_full
	ld hl, .BikeVoucherText
	call PrintText
	SetEvent EVENT_GOT_BIKE_VOUCHER
	jr .done
.bag_full
	ld hl, .BagFullText
	call PrintText
	jr .done
.nothanks
	ld hl, .NoStoryText
	call PrintText
	jr .done
.nothingleft
	ld hl, .FinalText
	call PrintText
.done
	jp TextScriptEnd

.IntroText:
	text_far _PokemonFanClubChairmanIntroText
	text_end

.StoryText:
	text_far _PokemonFanClubChairmanStoryText
	text_end

.BikeVoucherText:
	text_far _PokemonFanClubReceivedBikeVoucherText
	sound_get_key_item
	text_far _PokemonFanClubExplainBikeVoucherText
	text_end

.NoStoryText:
	text_far _PokemonFanClubNoStoryText
	text_end

.FinalText:
	text_far _PokemonFanClubChairFinalText
	text_end

.BagFullText:
	text_far _PokemonFanClubBagFullText
	text_end

PokemonFanClubReceptionistText:
	text_far _PokemonFanClubReceptionistText
	text_end

PokemonFanClubSign1Text:
	text_far _PokemonFanClubSign1Text
	text_end

PokemonFanClubSign2Text:
	text_far _PokemonFanClubSign2Text
	text_end
